/*
 * AnnoyODroid (c) by Jorge Antonio Diaz-Benito Soriano
 *
 * AnnoyODroid is licensed under a Creative Commons Attribution 4.0 International License.
 *
 * You should have received a copy of the license along with this work. If not, see http://creativecommons.org/licenses/by/4.0/.
 */

buildscript {
    repositories {
        jcenter()
    }

    //noinspection GroovyAssignabilityCheck
    dependencies {
        def classpathDependencies = rootProject.ext.classpathDependencies

        classpath classpathDependencies.gradleVersions
        classpath classpathDependencies.jsonSchema2Pojo
        classpath classpathDependencies.coveralls
    }
}

//apply from: "model/generate.gradle"

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion rootProject.ext.jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

apply plugin: 'maven'

group = 'com.github.stoyicker'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'Creative Commons Attribution 4.0 International'
                    url 'https://creativecommons.org/licenses/by/4.0/legalcode'
                    distribution 'repo'
                }
            }
        }
    }
}

//noinspection GroovyAssignabilityCheck
dependencies {
    def runDependencies = rootProject.ext.runDependencies
    def testDependencies = rootProject.ext.testDependencies

    compile runDependencies.commonsLang
    compile runDependencies.retrofitGsonConverter
    compile runDependencies.retrofit
    compile runDependencies.retrofitRxJavaAdapter
    compile runDependencies.javaXValidationApi
    compile runDependencies.glassfishAnnotations

    testCompile testDependencies.jUnit4
}
