buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.android.tools.build:gradle:1.4.0-beta1'
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:0.4.14'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'jsonschema2pojo'

group = 'com.github.stoyicker'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 6
        versionName "0.0.6"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            testCoverageEnabled true
        }
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
}

/*
 * AnnoyODroid (c) by Jorge Antonio Diaz-Benito Soriano
 *
 * AnnoyODroid is licensed under a Creative Commons Attribution 4.0 International License.
 *
 * You should have received a copy of the license along with this work. If not, see http://creativecommons.org/licenses/by/4.0/.
 */

//jsonSchema2Pojo {
//    generateBuilders = false
//    usePrimitives = true
//    source = files("src/main/json")
//    targetDirectory = file("build/generated-sources/js2p")
//    targetPackage = 'annoyodroid.datamodel'
//    propertyWordDelimiters = [] as char[]
//    useLongIntegers = true
//    useDoubleNumbers = true
//    includeHashcodeAndEquals = true
//    includeToString = true
//    annotationStyle = 'gson'
//    customAnnotator = 'org.jsonschema2pojo.NoopAnnotator'
//    includeJsr303Annotations = false
//    sourceType = 'json'
//    removeOldOutput = true
//    outputEncoding = 'UTF-8'
//    useJodaDates = false
//    useCommonsLang3 = true
//    initializeCollections = true
//    parcelable = false
//}